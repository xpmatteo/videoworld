apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'org.akhikhl.gretty'

group = 'com.thoughtworks.videorental'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

repositories {
    mavenCentral()
}

gretty {
    contextPath = '/'
    integrationTestTask = 'cucumber'
}

sourceSets {
    cucumber {
        java {
            srcDir 'src/cucumber/src/test/java'
        }

        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.compileClasspath
    }
}

dependencies {
    compile group: 'org.hibernate', name: 'hibernate-core', version: '3.6.7.Final'
    compile group: 'org.apache.struts', name: 'struts2-core', version: '2.1.8.1'

    compile files('lib/runtime/tw-commons-ddd-1.0.jar')
    compile group: 'joda-time', name: 'joda-time', version: '1.6'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.3.v20170317'
    compile group: 'org.eclipse.jetty', name: 'jetty-webapp', version: '9.4.3.v20170317'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    compile group: 'org.springframework', name: 'spring-core', version: '2.5.6'
    compile group: 'org.springframework', name: 'spring-beans', version: '2.5.6'
    compile group: 'org.springframework.javaconfig', name: 'spring-javaconfig', version: '1.0.0.m3'

    providedCompile 'javax.servlet:servlet-api:2.5'

    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.jsoup', name: 'jsoup', version: '1.10.2'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'info.cukes:cucumber-java:1.2.5'
    testCompile 'info.cukes:cucumber-junit:1.2.5'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '2.53.0'
}

task cucumber(type: Test) {
    description = 'Runs End to End Tests'
    testClassesDir = sourceSets.cucumber.output.classesDir
    classpath += sourceSets.cucumber.runtimeClasspath
    testLogging.showStandardStreams = true
    systemProperties System.getProperties()
}

test {
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat = 'full'
        afterSuite { desc, result ->
            if (!desc.parent) { // will only run for the outermost suite
                println "${result.testCount} test run, ${result.successfulTestCount} succeeded, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped"
            }
        }
    }
}

